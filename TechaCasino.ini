[script]
n0=/*
n1=TecheCasino
n2=A casino you can run from your Twitch chat!
n3=
n4=- created by Techaholica http://techaholica.com
n5=
n6=*/
n7=
n8=
n9=on *:LOAD: {
n10=  setVariables
n11=  // startTimers
n12=}
n13=
n14=on *:JOIN:#:{
n15=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n16=
n17=  if (!$exists(%userFile)) {
n18=    // if the user doesn't already have a profile create one
n19=    createUserFile $nick
n20=  }
n21=
n22=  startUserTimer
n23=}
n24=
n25=on *:PART:#:{
n26=  stopUserTimer
n27=}
n28=
n29=// creates the user's starting file
n30=alias -l createUserFile {
n31=  var %userFile $+(%baseDir,\users\,$1,.ini)
n32=  writeini %userFile character cash 500
n33=  writeini %userFile character drunk 0
n34=  writeini %userFile character items 0
n35=  writeini %userFile character bet 10
n36=  writeini %userFile character slotPlays 0
n37=  writeini %userFile character blackjackHands 0
n38=}
n39=alias -l startUserTimer {
n40=  timer $+ $nick 0 %userTimer setUserStats $nick
n41=}
n42=
n43=alias -l stopUserTimer {
n44=  timer $+ $nick off
n45=}
n46=
n47=alias -l setUserStats {
n48=  setStats $1 cash %userTimerCashReward
n49=  setStats $1 drunk %userTimerSoberReward
n50=}
n51=
n52=alias -l startTimers {
n53=  // nothing to do here for now
n54=}
n55=
n56=// a function to add whatever stat on the user profile. Used by other functions
n57=// $1 = username, $2 = property to modify, $3 = increment amount
n58=alias -l setStats {
n59=  var %userFile $+(%baseDir,\users\,$1,.ini)
n60=  if ($exists(%userFile)) {
n61=    var %property $readini(%userFile, n, character, $2)
n62=    var %result $calc(%property + $3)
n63=
n64=    if (%result < 0) {
n65=      // shouldn't be any values that go below 0
n66=      var %result 0
n67=    }
n68=
n69=    writeini %userFile character $2 %result
n70=  }
n71=}
n72=
n73=alias -l setVariables {
n74=  // mix of variables from config and manually set
n75=  // if you have this bot on multiple channel, it will fail because the Varaible file is vars.ini for both
n76=  // you should either write your variables to a different vars.ini file or change the below variables to 
n77=  // do something like this %gid#myotherchannel
n78=  // that way each channel has its own set of global variables
n79=
n80=  // set config settings  
n81=  Set -e %configFile TechaCasino\TechaCasino.config
n82=  Set -e %gameTitle $readini(%configFile, n, config, gameTitle)
n83=  Set -e %welcomeMessage $readini(%configFile, n, config, welcomeMessage)
n84=  Set -e %cashAddAmount $readini(%configFile, n, config, cashAddAmount)
n85=  Set -e %baseDir $readini(%configFile, n, config, baseDir)
n86=  Set -e %maxPosDrunk $readini(%configFile, n, config, maxPosDrunk)
n87=  Set -e %minPosDrunk $readini(%configFile, n, config, minPosDrunk)
n88=  Set -e %minNegDrunk $readini(%configFile, n, config, minNegDrunk)
n89=  Set -e %maxNegDrunk $readini(%configFile, n, config, maxNegDrunk)
n90=  Set -e %minPosSober $readini(%configFile, n, config, minPosSober)
n91=  Set -e %maxPosSober $readini(%configFile, n, config, maxPosSober)
n92=  Set -e %minNegSober $readini(%configFile, n, config, minNegSober)
n93=  Set -e %maxNegSober $readini(%configFile, n, config, maxNegSober)
n94=  Set -e %userTimer $readini(%configFile, n, config, userTimer)
n95=  Set -e %userTimerCashReward $readini(%configFile, n, config, userTimerCashReward)
n96=  Set -e %userTimerSoberReward $readini(%configFile, n, config, userTimerSoberReward)
n97=  Set -e %inventoryMax $readini(%configFile, n, config, inventoryMax)
n98=  Set -e %freeDrinkNumber $readini(%configFile, n, config, freeDrinkNumber)
n99=
n100=  // this is needed to support the TechaDrunk bot that plays the Casino
n101=  Set -e %techaDrinks 0
n102=}
n103=
n104=alias -l useMoney {
n105=  if ($1 == $null) {
n106=    // should never get here
n107=    msg ERROR: useMoney - Need to specify money amount
n108=    return $false
n109=  } 
n110=  else {
n111=    var %price $1
n112=
n113=    if (%price isnum) {
n114=      var %userFile $+(%baseDir,\users\,$nick,.ini)
n115=      var %cash $readini(%userFile, n, character, cash)
n116=
n117=      if (%cash < %price) {
n118=        msg $chan $nick You don't have enough money for that.
n119=        return $false
n120=      } 
n121=      else {
n122=        dec %cash %price
n123=        writeini %userFile character cash %cash
n124=        return $true
n125=      }
n126=    } 
n127=    else {
n128=      msg $chan $nick $+(%price, $chr(32), is not a number)
n129=    }
n130=  }
n131=}
n132=
n133=// $1 = item file, $2 = user
n134=// if $2 is null use the current user
n135=alias -l addInventoryItem {
n136=  if ($1 == $null) {
n137=    msg $chan $nick You must specify an item to add to your inventory
n138=  } 
n139=  else {
n140=    if ($2 == $null) {
n141=      var %userFile $+(%baseDir,\users\,$nick,.ini)
n142=    } 
n143=    else {
n144=      var %userFile $+(%baseDir,\users\,$2,.ini)    
n145=    }
n146=    var %items $readini(%userFile , n, character, items)
n147=
n148=    inc %items
n149=    writeini %userFile character items %items
n150=    writeini %userFile inventory %items $1
n151=  } 
n152=}
n153=
n154=// find item in current users inventory
n155=// $1 = short name of item
n156=alias -l getItem {
n157=  if ($1 != $null ) {
n158=    var %userFile $+(%baseDir,\users\,$nick,.ini)
n159=    var %items $readini(%userFile, n, character, items)
n160=    var %i 1
n161=
n162=    while (%i <= %items ) {
n163=      var %itemFile $readini(%userFile , n, inventory, %i)
n164=      var %short $readini(%itemFile, n, object, short)
n165=
n166=      if (%short == $1) {
n167=        // if we find the item, return the item file
n168=        return %itemFile
n169=      }
n170=
n171=      inc %i
n172=    }
n173=
n174=    // if we go through all the items and don't find what we are looking for, return $null
n175=    return $null
n176=  } 
n177=  else {
n178=    msg $chan $nick You must specify an item to check in your inventory
n179=    return $null
n180=  }
n181=}
n182=
n183=alias -l drink {
n184=  // some logic for the TechaDrunk bot
n185=  if ($nick == Techadrunk) {
n186=    inc %techaDrinks
n187=    write -c TechaDrinks.txt %techaDrinks
n188=    addInventoryItem $1
n189=  }
n190=
n191=  if ($1 != $null) {
n192=    var %drinkable $readini($1, n, item, drinkable)
n193=
n194=    if (%drinkable == yes) {
n195=      var %alcohol $readini($1, n, object, alcohol)
n196=
n197=      // be sure to update successfulDrink to only take one argument. Strength isn't a factor anymore
n198=      var %succesfulDrink $updateDrunk(%alcohol)
n199=      if (%succesfulDrink) {
n200=        removeItem $1
n201=      }
n202=    } 
n203=    else {
n204=      msg $chan $nick You can't drink that.
n205=    }
n206=  } 
n207=  else {
n208=    msg $chan $nick You must specify something to drink
n209=  }
n210=}
n211=
n212=/*
n213=- rolling is 1-100. 1-50 is a positive, 51+ is a negative
n214=- for adding drunk points, positive is adding 1-5 points, negative is 6-15 points
n215=- for getting sober positive is removing 6-10 points, and negative is removing 1-5 points
n216=- it is easier to get drunk than it is to get sober
n217=- getting 100 drunk kills you :(
n218=*/
n219=
n220=alias -l updateDrunk {
n221=  // $1 = alcohol (yes|no)
n222=  if ($1 != $null) {
n223=    var %userFile $+(%baseDir,\users\,$nick,.ini)
n224=    var %drunk $readini(%userFile , n, character, drunk)
n225=    var %points 0
n226=
n227=    if (($1 == no) && (%drunk == 0)) {
n228=      msg $chan $nick There is no need to consume the item. You are already completely sober.
n229=      return $false
n230=    } 
n231=    elseif ($1 == no) {
n232=      // this is a non alcoholic item so see how many drunk points to remove
n233=      // get a random number from 1-100. 1-50 is positive
n234=      var %result $rand(1,100)
n235=
n236=      if (%result <= 50) {
n237=        // this is a positive outcome so remove from points from the drunk stat
n238=        var %points $rand(%minPosSober,%maxPosSober)
n239=        var %quantifier much
n240=      } 
n241=      else {
n242=        // this is a negative result so only remove a small amount of points
n243=        var %points $rand(%minNegSober,%maxNegSober)  
n244=        var %quantifier slightly
n245=      }
n246=
n247=      dec %drunk %points 
n248=      if (%drunk < 0) {
n249=        // can't get negative drunk
n250=        var %drunk 0
n251=      }
n252=
n253=      writeini %userFile character drunk %drunk
n254=      msg $chan $+($nick, $chr(32), You consume the item and feel,  $chr(32), %quantifier, $chr(32), more sober. New drunk level:, $chr(32), %drunk)
n255=
n256=      return $true
n257=    } 
n258=    elseif ($1 == yes) {
n259=      // this is a an alcoholic item so see how many drunk points to add
n260=      // get a random number from 1-100. 1-50 is positive
n261=      var %result $rand(1,100)
n262=
n263=      if (%result <= 50) {
n264=        // this is a positive outcome so add a few points to the drunk stat
n265=        var %points $rand(%minPosDrunk,%maxPosDrunk)
n266=        var %quantifier somewhat
n267=      } 
n268=      else {
n269=        // this is a negative result so add a lot of points
n270=        var %points $rand(%minNegDrunk, %maxNegDrunk)  
n271=        var %quantifier much
n272=      }
n273=
n274=      inc %drunk %points 
n275=      if (%drunk >= 100) {
n276=        // You die :(
n277=        killCharacter
n278=        return $false
n279=      } 
n280=      else {
n281=        writeini %userFile character drunk %drunk
n282=        msg $chan $+($nick, $chr(32), You consume the beverage and feel,  $chr(32), %quantifier, $chr(32), more drunk. New drunk level:, $chr(32), %drunk)
n283=
n284=        // some logic for the TechaDrunk bot
n285=        if ($nick == TechaDrunk) {
n286=          write -c TechaDrunk.txt %drunk
n287=        }
n288=
n289=        return $true
n290=      }
n291=    } 
n292=    else {
n293=      msg ERROR: updateDrunk - updating drunk status: Unknown alcohol type
n294=      return $false
n295=    }
n296=  } 
n297=  else {
n298=    msg ERROR: updateDrunk - updating drunk status
n299=    return $false
n300=  }
n301=}
n302=
n303=/*
n304=Loop through the items and keep an array of all the items in the list
n305=if the item is found, remove the item and decrement the overall item count
n306=after all the inventory has been put into the array, write back to the inventory list
n307=using new inventory slot numbers
n308=*/
n309=alias -l removeItem {
n310=  if ($1 != $null) {
n311=    var %itemArray
n312=    var %found $false
n313=    var %userFile $+(%baseDir,\users\,$nick,.ini)
n314=    var %items $readini(%userFile, n, character, items)
n315=    var %i 1
n316=    var %index 1
n317=
n318=    while (%i <= %items ) {
n319=      var %itemFile $readini(%userFile , n, inventory, %i)
n320=
n321=      if ((%itemFile == $1) && (%found == $false)) {
n322=        // flag that we found the item
n323=        var %found $true
n324=      } 
n325=      else {
n326=        // if the items don't match, catalogue the item
n327=        var %itemArray. $+ %index %itemFile
n328=        inc %index
n329=      }
n330=      inc %i
n331=    }
n332=    if (%found) {
n333=      // at this point we have all the items of the inventory minus the one item
n334=      // add all the items back to inventory and write the new item count
n335=      dec %items
n336=      remini %userFile inventory
n337=      var %x 1
n338=      while (%x <= %items ) {
n339=        var %addItem $($+(%,itemArray.,%x),2)
n340=        writeini %userFile inventory %x %addItem 
n341=        inc %x
n342=      }
n343=      writeini %userFile character items %items
n344=    } 
n345=    else {
n346=      msg $chan $nick Could not find item in inventory: $1
n347=    }
n348=  } 
n349=  else {
n350=    msg $chan $nick You must specify something to remove
n351=  }
n352=}
n353=
n354=/*
n355=Doesn't actually kill the character. They just pass out and get their drunk
n356=value set back to 0. Could add death and other penalties later
n357=*/
n358=alias -l killCharacter {
n359=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n360=  var %class $readini(%userFile, n, character, class)
n361=
n362=  // reset drunk to 0
n363=  writeini %userFile character drunk 0  
n364=  msg $chan $nick throws up everywhere and passes out before waking up moments later to rally!
n365=
n366=  // add some stupid logic for TechaDrunk
n367=  if ($nick == Techadrunk) {
n368=    writeini %userFile character cash 1000
n369=    write -c TechaDrunk.txt 0
n370=    write -c TechaDrinks.txt 0
n371=    Set %techaDrinks 0
n372=  }
n373=}
n374=
n375=/*
n376=- random chance
n377=- icons: bell, star, horseshoe, bar, cherry
n378=- odds per symbol: 20, 15, 10, 5, 1
n379=- payouts: bet, betx2, betx3, betx5, betx100
n380=- every 10 bets gets 1 beer free
n381=*/
n382=alias -l playSlots {
n383=  var %slotsFile $+(%baseDir,\objects\slots.ini)
n384=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n385=  var %bet $readini(%userFile, n, character, bet)
n386=
n387=  // handle the free drinks for gambling
n388=  var %slotPlays $readini(%userFile, n, character, slotPlays)
n389=
n390=  if (%slotPlays == $null ) {
n391=    var %slotPlays 1
n392=  } 
n393=  else {
n394=    inc %slotPlays 
n395=  }
n396=
n397=  if (%slotPlays >= %freeDrinkNumber) {
n398=    msg $chan $nick A waitress comes by and hands you a [lightbeer]
n399=    writeini %userFile character slotPlays 0
n400=    var %lightbeerFile $+(%baseDir,\objects\lightbeer.ini)
n401=    addInventoryItem %lightbeerFile
n402=  }  
n403=  else {
n404=    writeini %userFile character slotPlays %slotPlays 
n405=  }
n406=
n407=  // get 3 random symbols
n408=  %x = 1
n409=  while (%x <= 3) {
n410=    var %random $rand(1,51)
n411=
n412=    if (%random <= 20) {
n413=      var %symbol. $+ %x 2
n414=      var %icon. $+ %x *bell*
n415=    } 
n416=    elseif ((%random > 20) && (%random <= 35)) {
n417=      var %symbol. $+ %x 5
n418=      var %icon. $+ %x *star*
n419=    }
n420=    elseif ((%random > 35) && (%random <= 45)) {
n421=      var %symbol. $+ %x 8
n422=      var %icon. $+ %x *horseshoe*
n423=    }
n424=    elseif ((%random > 45) && (%random <= 50)) {
n425=      var %symbol. $+ %x 10
n426=      var %icon. $+ %x *bar*
n427=    } 
n428=    else {
n429=      // this is the jackpot symbol!
n430=      var %symbol. $+ %x 100
n431=      var %icon. $+ %x *cherry*
n432=    }
n433=
n434=    inc %x
n435=  }
n436=
n437=  var %output $+(%icon.1,-,%icon.2,-,%icon.3,. )
n438=
n439=  // now lets see if we are a winner
n440=  if (( %symbol.1 == %symbol.2) && (%symbol.2 == %symbol.3)) {
n441=    var %winnings $calc(%bet * %symbol.1) 
n442=    if (%symbol.1 == 100) {
n443=      var %output $+(%output, $chr(32), $nick, $chr(32),You win the jackpot!!! Winnings: , %winnings)
n444=    } 
n445=    else {
n446=      var %output $+(%output, $chr(32), $nick, $chr(32), Winner! Winnings: , %winnings)    
n447=    }
n448=
n449=    // payout the winnings
n450=    var %cash $readini(%userFile , n, character, cash)
n451=    inc %cash %winnings
n452=    writeini %userFile character cash %cash
n453=  }
n454=  else {
n455=    var %output $+(%output, $chr(32), $nick, $chr(32), Sorry you didn't win. Try again!)      
n456=  }
n457=
n458=  msg $chan %output
n459=}
n460=
n461=/*
n462=- Blackjack with infinite decks. No card counting here!
n463=- no double down, split, or insurance
n464=*/
n465=alias -l playBlackjack {
n466=
n467=  var %blackjackFile $+(%baseDir,\objects\blackjack.ini)
n468=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n469=  var %bet $readini(%userFile, n, character, bet)
n470=  var %cash $readini(%userFile, n, character, cash)
n471=  var %blackJackHands $readini(%userFile, n, character, blackjackHands)
n472=
n473=  // first handle the free drinks at %freeDrinkNumber
n474=  if (%blackJackHands == $null ) {
n475=    var %blackjackHands 1
n476=  } 
n477=  else {
n478=    inc %blackjackHands
n479=  }
n480=
n481=  if (%blackJackHands >= %freeDrinkNumber) {
n482=    msg $chan $nick A waitress comes by and hands you a [lightbeer]
n483=    writeini %userFile character blackjackHands 0
n484=    var %lightbeerFile $+(%baseDir,\objects\lightbeer.ini)
n485=    addInventoryItem %lightbeerFile
n486=  }  
n487=  else {
n488=    writeini %userFile character blackjackHands %blackjackHands
n489=  }
n490=
n491=  writeini %userFile Blackjack card 0
n492=  writeini %userFile Blackjack total 0
n493=  writeini %userFile DealerBlackjack card 0
n494=  writeini %userFile DealerBlackjack total 0
n495=
n496=  // draw two cards. after the second card we have our total
n497=  var %total $drawCard(user)
n498=  var %total $drawCard(user)
n499=
n500=  if (%total == 21) {
n501=    var %winnings $calc(%bet * 3 / 2)
n502=    // don't forget to include your initial bet which is taken away when you start the game
n503=    inc %winnings %bet 
n504=    var %winnings $round(%winnings, 2)
n505=    inc %cash %winnings
n506=    writeini %userFile character cash %cash
n507=    msg $chan $nick You hit a blackjack on the first hand! Winnings: %winnings
n508=
n509=    // clear out the cards
n510=    remini %userFile BlackJack
n511=    remini %userFile DealerBlackJack
n512=  } 
n513=  else {
n514=    var %card.1 $readini(%userFile , n, Blackjack, 1)
n515=    var %card.2 $readini(%userFile , n, Blackjack, 2)
n516=
n517=    msg $chan $nick You have %card.1 and %card.2 for a total of %total
n518=
n519=    // draw two cards and get total score
n520=    var %dealerTotal $drawCard(dealer)
n521=    var %dealerTotal $drawCard(dealer)
n522=
n523=    if (%dealerTotal == 21) {
n524=      msg $chan $nick Dealer hit 21. You lose.
n525=
n526=      // clear out the cards
n527=      remini %userFile BlackJack
n528=      remini %userFile DealerBlackJack
n529=    } 
n530=    else {
n531=      var %dealerCard.1 $readini(%userFile , n, DealerBlackjack, 1)
n532=      var %dealerCard.2 $readini(%userFile , n, DealerBlackjack, 2)
n533=
n534=      msg $chan $nick Dealer has %dealerCard.1 showing.
n535=    }
n536=  }
n537=}
n538=
n539=// pick a card then return the total
n540=alias -l drawCard {
n541=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n542=  if ($1 == user) {
n543=    // update for user
n544=    var %section BlackJack
n545=  } 
n546=  else {
n547=    // update for dealer
n548=    var %section DealerBlackjack
n549=  }
n550=
n551=  var %total  $readini(%userFile, n, %section, total)
n552=  var %cardNumber $readini(%userFile , n, %section, card)
n553=
n554=  // draw a random card
n555=  var %card $rand(2,14)
n556=  // handle the cases where its a face card
n557=  if (%card == 11) {
n558=    var %cardPic J
n559=    var %cardValue 10
n560=  } 
n561=  elseif (%card == 12) {
n562=    var %cardPic Q
n563=    var %cardValue 10
n564=  }
n565=  elseif (%card == 13) {
n566=    var %cardPic K
n567=    var %cardValue 10
n568=  }
n569=  elseif (%card == 14) {
n570=    var %cardPic A
n571=    var %cardValue 11
n572=
n573=    // handle the case where 11 puts the user over 21   
n574=    var %tempTotal $calc(%total + 11)
n575=    if (%tempTotal > 21 ) {
n576=      var %cardValue 1
n577=    }
n578=  } 
n579=  else {
n580=    var %cardPic %card
n581=    var %cardValue %card
n582=  }
n583=
n584=  inc %total %cardValue
n585=  inc %cardNumber
n586=  writeini %userFile %section card %cardNumber
n587=  writeini %userFile %section total %total
n588=  writeini %userFile %section %cardNumber %cardPic
n589=
n590=  return %total
n591=}
n592=
n593=// draw a card and see if win or bust
n594=alias -l blackjackHit {
n595=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n596=  var %bet $readini(%userFile , n, character, bet)
n597=  var %cash $readini(%userFile , n, character, bet)
n598=
n599=  var %total $drawCard(user)
n600=
n601=  if (%total > 21) {
n602=    msg $chan $nick You busted with a total of %total
n603=    remini %userFile BlackJack
n604=    remini %userFile DealerBlackJack
n605=  } 
n606=  else {
n607=    var %cards $readini(%userFile , n, BlackJack, card)
n608=    var %x 1
n609=    var %output You have  
n610=    while (%x <= %cards) {
n611=      var %card. $+ %x $readini(%userFile , n, BlackJack, %x)
n612=      var %cardPic $($+(%,card.,%x),2)
n613=      var %output $+(%output,$chr(32),%cardPic, $chr(32)) 
n614=      inc %x
n615=    }
n616=    var %output $+(%output,$chr(32),For a total of, $chr(32), %total) 
n617=    msg $chan $nick %output
n618=  } 
n619=}
n620=
n621=alias -l blackjackStand {
n622=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n623=  var %bet $readini(%userFile , n, character, bet)
n624=  var %cash $readini(%userFile , n, character, cash)
n625=  var %total $readini(%userFile , n, DealerBlackJack, total)
n626=  var %userTotal $readini(%userFile , n, BlackJack, total)
n627=
n628=  while (%total < 17) {
n629=    var %total $drawCard(dealer)
n630=  }
n631=
n632=  if ((%total > %userTotal) && (%total <= 21)) {
n633=    msg $chan $nick You lose. Dealer hit %total
n634=  } 
n635=  elseif (%total > 21) {
n636=    // your winnings are your current bet back plus what you bet before
n637=    var %winnings %bet
n638=    inc %winnings %bet
n639=    var %winnings $round(%winnings, 2)
n640=    inc %cash %winnings
n641=    writeini %userFile character cash %cash 
n642=    msg $chan $nick You win! Dealer busts with %total $+ ! Winnings: %winnings
n643=  } 
n644=  else {
n645=    // it's a push then
n646=    inc %cash %bet
n647=    writeini %userFile character cash %cash 
n648=
n649=    msg $chan $nick Push. You get your bet back. 
n650=  }
n651=
n652=  // clear out the cards
n653=  remini %userFile BlackJack
n654=  remini %userFile DealerBlackJack
n655=}
n656=
n657=// do a check to see if we have room to add an item
n658=// $1 = user, if null use current user
n659=alias -l checkInventorySpace {
n660=  if ($1 == $null) {
n661=    var %user $nick
n662=  } 
n663=  else {
n664=    var %user $1  
n665=  }
n666=  var %userFile $+(%baseDir,\users\,%user,.ini)
n667=  var %items $readini(%userFile, n, character, items)
n668=  if (%items < %inventoryMax) {
n669=    return $true
n670=  } 
n671=  else {
n672=    msg $chan $nick User %user does not have anymore room in their inventory
n673=    return $false
n674=  }
n675=}
n676=
n677=// admin function to add to a users property
n678=// $1 = user $2 = property $3 = amount
n679=alias -l addStats {
n680=  setStats $1 $2 $3
n681=  // msg $chan $nick $3 has been added to $2 for $1
n682=}
n683=
n684=// !setbet BET_NUMBER PAYOUT DESCRIPTION 
n685=alias -l setBet {
n686=  var %bookieFile $+(%baseDir,\objects\bookie.ini)
n687=  var %betFile $+(%baseDir,\bets.ini)
n688=
n689=  writeini %bookieFile bets $1 $3-
n690=  writeini %bookieFile payouts $1 $2
n691=  var %desc $3- $+ (Paying x $+ $2)
n692=  writeini %betFile bets $1 %desc
n693=
n694=  msg $chan Bet has been set on bet number: $1
n695=}
n696=
n697=// !endBet BET_NUMBER OUTCOME 
n698=alias -l endBet {
n699=  var %bookieFile $+(%baseDir,\objects\bookie.ini)
n700=  var %betFile $+(%baseDir,\bets.ini)
n701=  var %desc $readini(%bookieFile, n, bets, $1)
n702=  var %newDesc %desc ***Outcome: $2
n703=
n704=  writeini %bookieFile outcome $1 $2
n705=  writeini %betFile bets $1 %newDesc
n706=
n707=  // now pay out all the users
n708=  updateBet payout $1 $2
n709=  msg $chan All users who bet * $2 * on bet * $1 * have been paid out now.
n710=}
n711=
n712=// !clearbet BET_NUMBER
n713=alias -l clearBet {
n714=  var %bookieFile $+(%baseDir,\objects\bookie.ini)
n715=  var %betFile $+(%baseDir,\bets.ini)
n716=
n717=  remini %bookieFile bets $1
n718=  remini %bookieFile payouts $1
n719=  remini %bookieFile outcome $1
n720=  remini %betFile bets $1
n721=
n722=  // now clear out all the user files
n723=  updateBet clear
n724=  msg $chan Cleared out bet number: $1
n725=}
n726=
n727=// helper function to cycle through and update user bets
n728=// $1 = a string specifying the action to be taken
n729=// clear = wipe the bet from the user file
n730=// payout = pay the user for the bet if they won
n731=alias -l updateBet {
n732=  var %index 1
n733=  var %dir  $+(%baseDir,\users)
n734=  while ($true ) {
n735=    var %file $findfile(%dir, *.ini, %index)
n736=    if (%file == $null) {
n737=      // if there are no more files break out of the loop
n738=      break
n739=    } 
n740=    else {
n741=      if ($1 == payout) {
n742=        // $2 = the bet number and $3 = the outcome
n743=        payoutBet %file $2 $3
n744=        // also now that they have been paid out, clear the bet
n745=        remini %file bets $1
n746=      } 
n747=      elseif ($1 == clear) {
n748=        remini %file bets $1
n749=      }
n750=    }
n751=
n752=    inc %index
n753=  }
n754=}
n755=
n756=// helper function to payout a user for their bet at the bookie
n757=// $1 = the path to the user's file
n758=// $2 = the bet number
n759=// $3 = the outcome
n760=alias -l payoutBet {
n761=  var %userBet $readini($1, n, bets, $2)
n762=
n763=  if ($3 == %userBet) {
n764=    // user won
n765=    var %bookieFile $+(%baseDir,\objects\bookie.ini)
n766=    var %userWager $readini($1, n, wager, $2)
n767=    var %multiplier $readini(%bookieFile , n, payouts, $2)
n768=    var %userCash $readini($1, n, character, cash)
n769=    var %userWinnings $calc(%userWager * %multiplier)
n770=
n771=    inc %cash %userWinnings
n772=    writeini $1 character cash %cash
n773=  } 
n774=  else {
n775=    //user lost
n776=  }
n777=
n778=  // win or lose clear out the bet
n779=  remini $1 bets $2
n780=  remini $1 wager $2
n781=
n782=}
n783=
n784=// closes the bet so no more users can bet on it
n785=// $1 = bet number
n786=alias -l closeBet {
n787=  var %bookieFile $+(%baseDir,\objects\bookie.ini)
n788=  var %betFile $+(%baseDir,\bets.ini)
n789=  var %desc $readini(%bookieFile, n, bets, $1)
n790=  var %newDesc %desc ***Betting is closed
n791=
n792=  writeini %bookieFile outcome $1 Closed
n793=  writeini %betFile bets $1 %newDesc
n794=
n795=  msg $chan Betting is now closed on bet number: $1
n796=}
n797=
n798=// place bet from user
n799=// $1 = bet number, $2 = bet, $3 = wager
n800=alias -l placeBet {
n801=  var %bookieFile $+(%baseDir,\objects\bookie.ini)
n802=  var %desc $readini(%bookieFile, n, bets, $1)
n803=  var %outcome $readini(%bookieFile, n, outcome, $1)
n804=
n805=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n806=  var %cash $readini(%userFile, n, character, cash)
n807=
n808=
n809=  if (%desc == $null) {
n810=    msg $chan $nick Not a valid bet number: $1
n811=
n812=    // give the user back his money on a bad bet
n813=    inc %cash $3
n814=    writeini %userFile character cash %cash
n815=  } 
n816=  elseif (%outcome != $null) {
n817=    msg $chan $nick This bet is not open: Bet number: $1
n818=
n819=    // give the user back his money on a bad bet
n820=    inc %cash $3
n821=    writeini %userFile character cash %cash
n822=  } 
n823=  else {
n824=    writeini %userFile bets $1 $2
n825=    writeini %userFile wager $1 $3
n826=    msg $chan $nick placed bet * $2 * on bet number * $1 *
n827=  }
n828=}
n829=
n830=// gives an item from one user to another
n831=// $1 = item, $2 = user
n832=alias -l giveItem {
n833=  // first check if the user has the item
n834=  var %itemFile $getItem($1)
n835=  if (%itemFile != $null) {
n836=    // check if the user receiving the item has inventory space
n837=    if ($checkInventorySpace($2)) {
n838=      // remove the item from the original user 
n839=      removeItem %itemFile
n840=
n841=      // give item to other user
n842=      addInventoryItem %itemfile $2
n843=      msg $chan $nick gave $1 to $2
n844=    }
n845=  } 
n846=  else {
n847=    msg $chan $nick Could not find item in your inventory: $1
n848=  }
n849=}
n850=
n851=/*
n852=All commands list here
n853=Read all text input starting with !
n854=*/
n855=on *:TEXT:!*:#: { 
n856=
n857=  // almost all comands need the user file so just set that up here
n858=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n859=
n860=  if ($1 == !dance) {
n861=    msg $chan $nick You do a little dance
n862=  } 
n863=  elseif ($1 == !addStats) {
n864=    if ($nick isop $chan) {
n865=      if (($2 == $null) || ($3 == $null) || ($4 == $null)) {
n866=        msg $chan $nick Usage: !addStats user property value
n867=      } 
n868=      else {
n869=        addStats $2 $3 $4
n870=      }
n871=    } 
n872=    else {
n873=      msg $chan $nick Sneaky! Sneaky! You must be an admin to use this command.
n874=    }
n875=  } 
n876=  elseif ($1 == !look) {
n877=    if ($2 == $null) {
n878=      var %locFile $+(%baseDir,\objects\TechaCasino.ini)
n879=      var %description $readini(%locFile, n, TechaCasino, description)
n880=      msg $chan $nick %description
n881=    } 
n882=    else {
n883=      var %objFile $+(%baseDir,\objects\,$2,.ini)
n884=      var %description  $readini(%objFile, n, object, description)
n885=      if (%description == $null) {
n886=        msg $chan $nick Can't find object: $2
n887=      } 
n888=      else {
n889=        msg $chan $nick %description
n890=      }
n891=    }
n892=  }
n893=  elseif ($1 == !buy) {
n894=    if ($2 == $null) {
n895=      msg $chan $nick You must specify an item to buy
n896=    } 
n897=    else {
n898=      var %objFile $+(%baseDir,\objects\,$2,.ini)
n899=      var %price $readini(%objFile, n, object, price)
n900=
n901=      if (%price == $null) {
n902=        msg $chan $nick Can't find object: $2
n903=      } 
n904=      else {
n905=        if ($checkInventorySpace) {
n906=          var %paid $useMoney(%price)
n907=
n908=          if (%paid) {
n909=            addInventoryItem %objFile
n910=            msg $chan $nick You purchased: $2
n911=          } 
n912=        }
n913=      }
n914=    }
n915=  }
n916=  elseif ($1 == !inventory) {
n917=    var %invCount $readini(%userFile , n, character, items)
n918=    var %output Inventory: 
n919=    var %i 1
n920=
n921=    while (%i <= %invCount ) {
n922=      var %itemFile $readini(%userFile , n, inventory, %i)
n923=      var %name $readini(%itemFile, n, object, name)
n924=
n925=      // some logic to take care of adding the commas
n926=      if (%i == 1 ) {
n927=        var %output $+(%output,$chr(32),%name)
n928=      } 
n929=      else {
n930=        var %output $+(%output,$chr(44),$chr(32),%name)
n931=      }
n932=      inc %i
n933=    }
n934=    // add current cash and drunk as well.
n935=    var %cash $readini(%userFile, n, character, cash)
n936=    var %drunk $readini(%userFile, n, character, drunk)
n937=
n938=    var %output $+(%output,$chr(44),$chr(32),Cash:, $chr(32), %cash, $chr(44),$chr(32), Drunk:, $chr(32), %drunk) 
n939=    msg $chan $nick %output
n940=  } 
n941=  elseif ($1 == !drink) {
n942=    if ($2 == $null) {
n943=      msg $chan What do you want to drink? Use !drink BEVERAGE
n944=    } 
n945=    else {
n946=      var %itemFile $getItem($2)
n947=      if (%itemFile != $null) {
n948=        drink %itemFile
n949=      } 
n950=      else {
n951=        msg $chan $nick Can't find $2 in your inventory
n952=      }
n953=    }
n954=  }
n955=  elseif ($1 == !drop) {
n956=    if ($2 == $null) {
n957=      msg $chan $nick What do you want to drop? Use !drop ITEM
n958=    } 
n959=    else {
n960=      var %itemFile $getItem($2)
n961=      if (%itemFile != $null) {
n962=        removeItem %itemFile
n963=        msg $chan $nick You drop: $2
n964=      } 
n965=      else {
n966=        msg $chan $nick Can't find $2 in your inventory
n967=      }
n968=    }
n969=  }
n970=  elseif ($1 == !bet) {
n971=    if (($2 != $null) && ($2 isnum)) {
n972=      if ($2 > 0) {
n973=        // handle the case where someone tries to chance their bet after getting drawn cards         
n974=        var %blackjackCard $readini(%userFile , n, BlackJack, card)
n975=
n976=        if (%blackjackCard == $null) {
n977=          var %bet $round($2, 2)
n978=
n979=          writeini %userFile character bet %bet 
n980=          msg $chan $nick You are now betting %bet for all gambling games
n981=        } 
n982=        else {
n983=          msg $chan $nick You cannot change your bet in the middle of a hand
n984=        }
n985=      }
n986=      else {
n987=        msg $chan $nick You must bet at least a dollar!
n988=      }
n989=    } 
n990=    else {
n991=      var %bet $readini(%userFile, n, character, bet)
n992=      if (%bet != $null) {
n993=        msg $chan $nick You are betting %bet      
n994=      } 
n995=      else {
n996=        // don't think you can ever get this
n997=        msg $chan $nick You don't have a current bet amount. Use !bet AMOUNT set an amount.
n998=      }
n999=    }
n1000=  } 
n1001=  elseif ($1 == !play) {
n1002=    if ($2 == $null) {
n1003=      msg $chan $nick Specify a game to play. !play GAME
n1004=    } 
n1005=    else {
n1006=      var %objFile $+(%baseDir,\objects\,$2,.ini)
n1007=      var %description  $readini(%objFile, n, object, description)
n1008=      if (%description == $null) {
n1009=        msg $chan $nick Can't find object: $2
n1010=      } 
n1011=      else {
n1012=        if ($2 == slots) {
n1013=          var %slotsFile $+(%baseDir,\objects\slots.ini)
n1014=          var %bet $readini(%userFile, n, character, bet)
n1015=
n1016=          var %paid $useMoney(%bet)
n1017=          if (%paid) {
n1018=            playSlots
n1019=          }
n1020=        }
n1021=        elseif ($2 == blackjack) {
n1022=          var %blackjackFile $+(%baseDir,\objects\blackjack.ini)
n1023=          var %bet $readini(%userFile , n, character, bet)
n1024=
n1025=          var %paid $useMoney(%bet)
n1026=          if (%paid) {
n1027=            playBlackjack
n1028=          }
n1029=        }
n1030=      }
n1031=    }
n1032=  } 
n1033=  elseif ($1 == !test) {
n1034=    // use this to test various things I may be working on
n1035=  } 
n1036=  elseif ($1 == !hit) {
n1037=    var %card $readini(%userFile, n, Blackjack, card)
n1038=
n1039=    if (%card == $null) {
n1040=      msg $chan $nick You must start a game of blackjack first. !play BlackJack
n1041=    } 
n1042=    else {
n1043=      blackjackHit
n1044=    }
n1045=  }
n1046=  elseif ($1 == !stand) {
n1047=    var %cards $readini(%userFile, n, Blackjack, card)
n1048=    if (%cards == $null) {
n1049=      msg $chan $nick You must start a game of blackjack first. !play BlackJack
n1050=    } 
n1051=    else {
n1052=      blackjackStand
n1053=    }
n1054=  } 
n1055=  elseif ($1 == !doubleDown) {
n1056=    var %cards $readini(%userFile, n, Blackjack, card)
n1057=    if (%cards == $null) {
n1058=      msg $chan $nick You must start a game of blackjack first. !play BlackJack
n1059=    }
n1060=    else {
n1061=      // double down means double the bet so double the bet, and subtract the extra from their cash
n1062=      var %cash $readini(%userFile, n, character, cash)
n1063=      dec %cash %bet
n1064=      if (%cash < 0) {
n1065=        msg $chan $nick You don't have enough money to double down
n1066=      } 
n1067=      else {
n1068=        // user has enough money so subtract the money and double the bet
n1069=        writeini %userFile character cash %cash 
n1070=        var %bet $readini(%userFile, n, character, bet)
n1071=        inc %bet %bet
n1072=        writeini %userFile character bet %bet 
n1073=
n1074=        // draw one card and check if bust
n1075=        blackjackHit
n1076=        // cards get wiped when you bust so check this value
n1077=        var %cards $readini(%userFile, n, Blackjack, card)
n1078=        if (%cards != $null) {
n1079=          // if you didn't bust, time for the dealer to play out his hand
n1080=          blackjackStand
n1081=        }
n1082=
n1083=        // reduce the bet once again
n1084=        dec %bet %bet
n1085=        writeini %userFile character bet %bet 
n1086=      }
n1087=    }
n1088=  } 
n1089=  elseif ($1 == !placebet) {
n1090=    if (($2 == $null) || ($3 == $null)) {
n1091=      msg $chan $nick What do you want to bet on? Use !placebet BET_NUMBER BET ( WAGER )
n1092=    } 
n1093=    else {
n1094=      if ($4 != $null) {
n1095=        if  ($4 isnum) {
n1096=          var %paid $useMoney($4)
n1097=          if (%paid) {
n1098=            placeBet $2 $3 $4 
n1099=          }
n1100=        } 
n1101=        else {
n1102=          msg $chan $nick you must specify a number for your wager
n1103=        }
n1104=      } 
n1105=      else {
n1106=
n1107=        var %wager $readini(%userFile, n, character, bet)
n1108=
n1109=        var %paid $useMoney(%wager)
n1110=        if (%paid) {
n1111=          placeBet $2 $3 %wager
n1112=        }
n1113=      }
n1114=    }
n1115=  } 
n1116=  elseif ($1 == !setbet) {
n1117=    if ($nick isop $chan) {
n1118=      if (($2 == $null) || ($3 == $null) || ($4 == $null)) {
n1119=        msg $chan $nick Usage: !setbet BET_NUMBER PAYOUT DESCRIPTION 
n1120=      } 
n1121=      else {
n1122=        setBet $2 $3 $4-
n1123=      }
n1124=    } 
n1125=    else {
n1126=      msg $chan $nick Sneaky! Sneaky! You must be an admin to use this command.
n1127=    }
n1128=
n1129=  }
n1130=  elseif ($1 == !endBet) {
n1131=    if ($nick isop $chan) {
n1132=      if (($2 == $null) || ($3 == $null)) {
n1133=        msg $chan $nick Usage: !setbet BET_NUMBER OUTCOME 
n1134=      } 
n1135=      else {
n1136=        endBet $2 $3
n1137=      }
n1138=    } 
n1139=    else {
n1140=      msg $chan $nick Sneaky! Sneaky! You must be an admin to use this command.
n1141=    }
n1142=  }
n1143=  elseif ($1 == !clearBet) {
n1144=    if ($nick isop $chan) {
n1145=      if ($2 == $null) {
n1146=        msg $chan $nick Usage: !clearbet BET_NUMBER 
n1147=      } 
n1148=      else {
n1149=        clearBet $2
n1150=      }
n1151=    } 
n1152=    else {
n1153=      msg $chan $nick Sneaky! Sneaky! You must be an admin to use this command.
n1154=    }
n1155=  }
n1156=  elseif ($1 == !closeBet) {
n1157=    if ($nick isop $chan) {
n1158=      if ($2 == $null) {
n1159=        msg $chan $nick Usage: !closeBet BET_NUMBER 
n1160=      } 
n1161=      else {
n1162=        closeBet $2
n1163=      }
n1164=    } 
n1165=    else {
n1166=      msg $chan $nick Sneaky! Sneaky! You must be an admin to use this command.
n1167=    }
n1168=  }
n1169=  elseif ($1 == !give) {
n1170=    if (($2 == $null) || ($3 == $null)) {
n1171=      msg $chan $nick Usage: !give OBJECT USERNAME 
n1172=    } 
n1173=    elseif (($3 == to) && ($4 == $null)) {
n1174=      msg $chan $nick Usage: !give OBJECT USERNAME 
n1175=    }
n1176=    else {
n1177=      if ($3 == to) {
n1178=        var %user $4
n1179=      } 
n1180=      else {
n1181=        var %user $3
n1182=      }
n1183=
n1184=      var %userFile $+(%baseDir,\users\,%user,.ini)
n1185=      if ($exists(%userFile)) {
n1186=        giveItem $2 %user    
n1187=      } 
n1188=      else {
n1189=        msg $chan $nick Can't find user %user
n1190=      }
n1191=    }
n1192=  }
n1193=  else {
n1194=    // do nothing   
n1195=  }
n1196=}
